window.Discourse.Site = Ember.Object.extend

  notificationLookup: (->
    result = Array()
    Object.keys @get('notification_types'), (k, v) -> result[v] = k
    result
  ).property('notification_types')

  flagTypes: (->
    postActionTypes = @get('post_action_types')
    return [] unless postActionTypes
    postActionTypes.filterProperty('is_flag', true)
  ).property('post_action_types.@each')

  postActionTypeById: (id) -> @get("postActionByIdLookup.action#{id}")
  

window.Discourse.Site.reopenClass

  create: (obj) ->
    Object.tap @_super(obj), (result) =>

      if result.categories
        result.categories = result.categories.map (c) => Discourse.Category.create(c)

      if result.post_action_types
        result.postActionByIdLookup = Em.Object.create()
        result.post_action_types = result.post_action_types.map (p) => 
          actionType = Discourse.PostActionType.create(p)
          result.postActionByIdLookup.set("action#{p.id}", actionType)          
          actionType


      if result.archetypes
        result.archetypes = result.archetypes.map (a) => Discourse.Archetype.create(a)

