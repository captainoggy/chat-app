window.Discourse.Category = Discourse.Model.extend

  url: (->
    "/category/#{@get('slug')}"
  ).property('name')

  style: (->
    "background-color: ##{@get('color')}"
  ).property('color')

  moreTopics: (->
    return @get('topic_count') > <%= SiteSetting.category_featured_topics %>
  ).property('topic_count')

  save: (args) ->

    url = "/categories"
    url = "/categories/#{@get('id')}" if @get('id')

    @ajax url,
      data:
        name: @get('name')
        color: @get('color')
      type: if @get('id') then 'PUT' else 'POST'
      success: (result) => args.success(result)
      error: (errors) => args.error(errors)

  delete: (callback) ->
    $.ajax "/categories/#{@get('slug')}",
      type: 'DELETE'
      success: => callback()    
